buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.cloud:spring-cloud-contract-gradle-plugin:3.1.1'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'maven-publish'
    id 'groovy'
}

apply plugin: 'spring-cloud-contract'

group = 'com.epam.msa'
version = '0.0.1'
sourceCompatibility = '16'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/release" }
}

ext {
    set('springCloudVersion', "2021.0.1")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.data:spring-data-rest-hal-explorer'
    implementation 'org.springframework.retry:spring-retry:1.3.2'

    implementation 'com.amazonaws:aws-java-sdk:1.11.852'
    implementation 'org.modelmapper:modelmapper:3.0.0'
    implementation 'commons-io:commons-io:2.10.0'
    implementation 'org.projectlombok:lombok:1.18.20'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'com.google.code.gson:gson:2.9.0'

    implementation 'io.cucumber:cucumber-java:7.2.3'
    implementation 'io.cucumber:cucumber-spring:7.2.3'
    compileOnly 'io.cucumber:cucumber-jvm-deps:1.0.6'

    testImplementation 'io.cucumber:cucumber-junit:7.2.3'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.8.2'
    testImplementation 'com.h2database:h2:2.1.212'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

contracts {
    testFramework = org.springframework.cloud.contract.verifier.config.TestFramework.JUNIT5
    testMode = 'MockMvc'
    packageWithBaseClasses = 'com.epam.msa.contracts'
}

publishing {
    publications {
        stubs(MavenPublication) {
            artifactId project.name
            artifact verifierStubsJar
        }
    }
}

tasks.named('contractTest') {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            boolean skipTests = project.findProperty('SKIP_TESTS') ? true : false
            if (result.testCount == 0 && !skipTests) {
                throw new IllegalStateException("No contract tests were found. Failing the build")
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            boolean skipTests = project.findProperty('SKIP_TESTS') ? true : false
            if (result.testCount == 0 && !skipTests) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}